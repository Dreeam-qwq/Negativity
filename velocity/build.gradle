import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
}

evaluationDependsOn(':common')

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    api project(':common')
    compileOnly 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesNotMatching('META-INF/services/*') {
        expand(props)
    }
}

compileJava {
    javaCompiler.set(
            javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(11) }
    )
}

shadowJar {
    archiveBaseName.set('NegativityVelocity')
    archiveAppendix.set(null)
    archiveClassifier.set(null)

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
